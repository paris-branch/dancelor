open NesUnix
open Dancelor_common

module Log = (val Logs.src_log @@ Logs.Src.create "server.controller.user": Logs.LOG)

let get env id =
  match%lwt Database.User.get id with
  | None -> Permission.reject_can_get ()
  | Some user ->
    Permission.assert_can_get_public env user;%lwt
    lwt user

let status = Environment.user

let sign_in env username password remember_me =
  Log.info (fun m -> m "Attempt to sign in with username `%s`." (Username.to_string username));
  match%lwt Environment.user env with
  | Some _user ->
    Log.info (fun m -> m "Rejecting because already signed in.");
    lwt_none
  | None ->
    (* FIXME: should be included in [get_password_from_username] except we return a user  *)
    match%lwt Database.User.get_from_username username with
    | None ->
      Log.info (fun m -> m "Rejecting because of wrong username.");
      lwt_none
    | Some user ->
      match%lwt Database.User.get_password_from_username username with
      | None ->
        Log.info (fun m -> m "Rejecting because user has no password.");
        lwt_none
      | Some hashedPassword when not @@ HashedSecret.is ~clear: (Entry.User.Password_clear.project password) (Database.User.Password_hashed.project hashedPassword) ->
        (* NOTE: Similar to other tokens, we should be able to compare directly but need to project. *)
        Log.info (fun m -> m "Rejecting because passwords do not match.");
        lwt_none
      | Some _ ->
        Environment.sign_in env user ~remember_me;%lwt
        Log.info (fun m -> m "Accepted sign in for %a." Environment.pp env);
        lwt_some user

let sign_out env =
  match%lwt Environment.user env with
  | None -> lwt_unit
  | Some user -> Environment.sign_out env user

let create env user =
  Permission.assert_can_administrate env @@ fun _admin ->
  let token = Model.User.Password_reset_token_clear.make () in
  (* NOTE: We should use Password_reset_token_hashed.make here, but HashedSecret.make
     is only available on the server side (NesHashedSecretUnix), not in common code. *)
  let password_reset_token_hash = Database.User.Password_reset_token_hashed.inject @@ HashedSecret.make ~clear: (Model.User.Password_reset_token_clear.project token) in
  let password_reset_token_max_date = Datetime.make_in_the_future (float_of_int @@ 3 * 24 * 3600) in
  let%lwt id =
    Database.User.create
      ~username: (Model.User.username user)
      ~role: (Model.User.role user)
      ~password_reset_token_hash
      ~password_reset_token_max_date
  in
  let%lwt user = Option.get <$> Database.User.get id in
  lwt (user, token)

let prepare_reset_password env username =
  Permission.assert_can_administrate env @@ fun _admin ->
  Log.info (fun m -> m "Preparing password reset for user `%s`." (Username.to_string username));
  match%lwt Database.User.get_from_username username with
  | None ->
    Log.info (fun m -> m "Rejecting because username not found.");
    Madge_server.shortcut_bad_request "User not found."
  | Some user ->
    let token = Model.User.Password_reset_token_clear.make () in
    (* NOTE: We should use Password_reset_token_hashed.make here, but HashedSecret.make
       is only available on the server side (NesHashedSecretUnix), not in common code. *)
    let hashed_token = Database.User.Password_reset_token_hashed.inject @@ HashedSecret.make ~clear: (Model.User.Password_reset_token_clear.project token) in
    let max_date = Datetime.make_in_the_future (float_of_int @@ 3 * 24 * 3600) in
    Database.User.set_password_reset_token (Entry.id user) hashed_token max_date;%lwt
    Log.info (fun m -> m "Password reset token generated for user `%s`." (Username.to_string username));
    lwt token

let reset_password username token password =
  Log.info (fun m -> m "Attempt to reset password for user `%s`." (Username.to_string username));
  (* FIXME: should be included in [get_password_reset_token_from_username] except we return a user  *)
  match%lwt Database.User.get_from_username username with
  | None ->
    Log.info (fun m -> m "Rejecting because of wrong username.");
    Madge_server.shortcut_forbidden_no_leak ()
  | Some user ->
    match%lwt Database.User.get_password_reset_token_from_username username with
    | None ->
      Log.info (fun m -> m "Rejecting because of lack of token.");
      Madge_server.shortcut_forbidden_no_leak ()
    | Some (_, token_date) when Datetime.(diff (now ())) token_date > 3. *. 24. *. 3600. ->
      Log.info (fun m -> m "Rejecting because token is too old.");
      Madge_server.shortcut_forbidden_no_leak ()
    | Some (hashed_token, _) ->
      (* NOTE: We should be able to compare Password_reset_token_clear with
         Password_reset_token_hashed directly, but we need to project because
         HashedSecret.is is only available on the server side. *)
      if not @@ HashedSecret.is ~clear: (Model.User.Password_reset_token_clear.project token) (Database.User.Password_reset_token_hashed.project hashed_token) then
        (
          Log.info (fun m -> m "Rejecting because tokens do no match.");
          Madge_server.shortcut_forbidden_no_leak ()
        )
      else
        (
          Log.info (fun m -> m "Accepting to reset password.");
          (* NOTE: We should use Password_hashed.make here, but HashedSecret.make
             is only available on the server side (NesHashedSecretUnix), not in common code. *)
          let password = Database.User.Password_hashed.inject @@ HashedSecret.make ~clear: (Model.User.Password_clear.project password) in
          Database.User.set_password (Entry.id user) password
        )

let set_omniscience env value =
  Permission.assert_can_administrate env @@ fun user ->
  Database.User.set_omniscience (Entry.id user) value

include Search.Build(struct
  type value = Model.User.entry
  type filter = (Model.User.t, Filter.User.t) Formula_entry.public

  let get_all env =
    let all = Database.User.get_all () in
    let stream = (Lwt_stream.filter_s (Permission.can_get_public env) % Lwt_stream.of_list) <$> all in
    Lwt_stream.flip_lwt stream

  let optimise_filter = Text_formula_converter.optimise (Formula_entry.converter_public Filter.User.converter)
  let filter_is_empty = (=) Formula.False
  let filter_accepts = Formula_entry.accepts_public Filter.User.accepts
  let score_true = Formula.interpret_true

  let tiebreakers =
    Lwt_list.[increasing (lwt % Username.to_string % Model.User.username') String.Sensible.compare]
end)

let dispatch : type a r. Environment.t -> (a, r Lwt.t, r) Endpoints.User.t -> a = fun env endpoint ->
  match endpoint with
  | Get -> get env
  | Status -> status env
  | Sign_in -> sign_in env
  | Sign_out -> sign_out env
  | Create -> create env
  | Prepare_reset_password -> prepare_reset_password env
  | Reset_password -> reset_password
  | Search -> search env
  | Set_omniscience -> set_omniscience env
