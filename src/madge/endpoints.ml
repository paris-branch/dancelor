open Serialisation

module type Internal_endpoints = sig
  type ('a, 'w, 'r) t
  (** The type of internal endpoints. Some other endpoints (batching, metrics)
      will be added automatically. *)

  val route_internal : ('a, 'w, 'r) t -> ('a, 'w, 'r) Route.t
  (** Routes of internal endpoints, without the [/api] prefix. *)

  (* FIXME: all the following should be generated by [@@deriving
     madge_wrapped_endpoints] in signatures *)
  type wrapped = W : ('a, 'r Lwt.t, 'r) t -> wrapped
  val all : wrapped list
  type 'w wrapped' = W' : ('a, 'w, 'r) t -> 'w wrapped'
  val all' : unit -> 'w wrapped' list
  val name : ('a, 'w, 'r) t -> string
end

module type Endpoints = sig
  include Internal_endpoints

  type (_, _, _) full =
    | Api : ('a, 'w, 'r) t -> ('a, 'w, 'r) full
    | Batch : (Request.t list -> 'w, 'w, Response.t list) full
    | Metrics : ('w, 'w, JVoid.t) full
  (** The type of full endpoints, which are the internal endpoints augmented of
      some other endpoints (batching, metrics). *)

  val route_full : ('a, 'w, 'r) full -> ('a, 'w, 'r) Route.t
  (** Routes for full endpoints; this is what is ultimately used to generate
      URIs or to match them. *)

  val route : ('a, 'w, 'r) t -> ('a, 'w, 'r) Route.t
  (** Routes for internal endpoints. [route e] is short for [route_full (Api e)]. *)
end

module Make_endpoints (Internal : Internal_endpoints) : Endpoints with
  type ('a, 'w, 'r) t = ('a, 'w, 'r) Internal.t
= struct
  include Internal

  type (_, _, _) full =
    | Api : ('a, 'w, 'r) t -> ('a, 'w, 'r) full
    | Batch : (Request.t list -> 'w, 'w, Response.t list) full
    | Metrics : ('w, 'w, JVoid.t) full

  let route_full : type a w r. (a, w, r) full -> (a, w, r) Route.t =
    let open Route in
    function
      | Api endpoint -> literal "api" @@ route_internal endpoint
      | Batch -> literal "api" @@ literal "batch" @@ body "requests" (module JList(Request)) @@ post (module JList(Response))
      | Metrics -> literal "api" @@ literal "metrics" @@ void ()

  let route endpoint = route_full (Api endpoint)
end
